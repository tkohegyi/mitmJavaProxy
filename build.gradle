/*==========================================================================
Author: Tamas Kohegyi
===========================================================================*/
buildscript {
    dependencies {
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
    }
}

plugins {
    id "org.sonarqube" version "3.2.0"
}

apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    maven { url "https://repo.maven.apache.org/maven2" }
    maven { url "https://repository.mulesoft.org/nexus/content/repositories/public/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

group = "website.magyar"
archivesBaseName = "mitm-java-proxy"
ext.componentDescription = "MITM Java Proxy"

ext.wilmaVersion=2.5
ext.myBuildNumber='SNAPSHOT'
if ( hasProperty('buildNumber') ) {
    myBuildNumber = "${project.ext.buildNumber}"
}
version = "$wilmaVersion" + ".26." + "${project.ext.myBuildNumber}"
mainClassName = "website.magyar.mitm.standalone.StandaloneProxy"

def isSnapshot = project.version.contains('SNAPSHOT')

// Source and target info
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

compileJava {
    options.encoding = "UTF-8"
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

// checkstyle settings
// general checkstyle availability:  https://checkstyle.sourceforge.io/releasenotes.html
// compatibility with sonar https://docs.sonarqube.org/latest/instance-administration/plugin-version-matrix/
checkstyle.toolVersion = '8.40'
checkstyle.ignoreFailures = true
checkstyle.configFile = file("$rootProject.rootDir/config/checkstyle/checkstyle.xml")
checkstyle.configProperties = ['samedir' : "$rootProject.rootDir/config/checkstyle"]

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "tkohegyi_mitmJavaProxy"
        property "sonar.organization", "tkohegyi"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.checkstyle.reportPaths", "build/reports/checkstyle/main.xml"
    }
}
//project.tasks["sonarqube"].onlyIf { project.hasProperty('systemProp.sonar.login') }

test {
//    jvmArgs '-Djavax.net.debug=all' // add line
    testLogging {
        events "skipped", "failed" //, "standardOut", "standardError"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        showStandardStreams = false
    }
}

test.finalizedBy jacocoTestReport

def configFileDefaultPath = "$project.projectDir/proxy.conf.properties"
if (!hasProperty("configFilePath")) {
    project.ext.configFilePath = configFileDefaultPath
}

run{
    args = ["$configFilePath"]
}

shadowJar {
    append('META-INF/spring.handlers')
    append('META-INF/spring.schemas')

    excludes = [
            'META-INF/*.SF',
            'META-INF/*.DSA',
            'META-INF/*.RSA',
            'images/',
            '*.txt',
            '*.gif',
            '*.png',
            '*.dtd',
            'build.properties',
            'XPP3_1.1.4c_MIN_VERSION',
            'activemq.xsd.*',
            'about.html'] as Iterable<String>
}

dependencies {
    implementation group: 'ch.qos.logback', name: 'logback-classic', version:'1.2.11'
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version:'1.7.36'
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version:'1.7.36'
    api group: 'org.apache.httpcomponents', name: 'httpmime', version:'4.5.13' //this includes httpclient too
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'org.brotli', name: 'dec', version: '0.1.2'
    implementation group: 'org.apache.ant', name: 'ant', version:'1.10.12'
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version:'1.71.1' //this includes bcprov-jdk18on too
    implementation group: 'org.eclipse.jetty', name: 'jetty-io', version: '9.4.48.v20220622'
    implementation group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    implementation group: 'dnsjava', name: 'dnsjava', version: '3.5.0'
    testImplementation group: 'junit', name: 'junit', version:'4.13.2'
    testImplementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    testImplementation group: 'org.mockito', name: 'mockito-all', version:'1.10.19'
    testImplementation group: 'io.netty', name: 'netty-all', version: '4.1.80.Final'
    testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.48.v20220622'
    testImplementation group: 'org.springframework', name: 'spring-core', version: '5.3.22'
    testImplementation group: 'com.nixxcode.jvmbrotli', name: 'jvmbrotli', version: '0.2.0'
    testImplementation group: 'com.nixxcode.jvmbrotli', name: 'jvmbrotli-win32-x86-amd64', version: '0.2.0'
    testImplementation group: 'com.nixxcode.jvmbrotli', name: 'jvmbrotli-linux-x86-amd64', version: '0.2.0'
}

def myCopySpec = project.copySpec {
    from(rootProject.rootDir.absolutePath + '/') {
        include 'sslSupport/cybervillainsCA.cer'
        include 'sslSupport/cybervillainsCA.jks'
    }
}

jar {
    manifest.attributes( 'provider': 'gradle')
    manifest.attributes( 'Main-Class': mainClassName)
    manifest.attributes( 'Implementation-Title': "$componentDescription - V${project.version}")
    with myCopySpec
    finalizedBy shadowJar
}

def myCopySpec2 = project.copySpec {
    from(rootProject.rootDir.absolutePath + '/build/libs/') {
        include "mitm-java-proxy-$version" + '-all.jar'
    }
    from(rootProject.rootDir.absolutePath + '/') {
        include 'LICENSE.txt'
        include 'README.txt'
        include 'README.md'
    }
}

task copyToReleaseRoot (type: Copy) {
    dependsOn shadowJar
    into(rootProject.rootDir.absolutePath + '/release')
    rename "mitm-java-proxy-$version" + '-all.jar', "mitm-java-proxy-" + "$version" + ".jar"
    with myCopySpec2
}

task release(type: Zip) {
    dependsOn copyToReleaseRoot
    from rootProject.rootDir.absolutePath + '/release'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            pom {
                name = 'MITM HTTP/HTTPS Java Proxy'
                description = 'HTTP/HTTPS MITM Java Proxy with the possibility of intercepting and investigating/altering both requests and responses.'
                url = 'https://github.com/tkohegyi/mitmJavaProxy/wiki'
                scm {
                    connection = 'https://github.com/tkohegyi/mitmJavaProxy.git'
                    developerConnection = 'https://github.com/tkohegyi/mitmJavaProxy.git'
                    url = 'https://github.com/tkohegyi/mitmJavaProxy/'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tkohegyi'
                        name = 'Tamas Kohegyi'
                        email = 'tkohegyi2@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def stagingRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url isSnapshot ? snapshotRepoUrl : stagingRepoUrl
            credentials {
                username = rootProject.findProperty('ossrhUsername') ?: ''
                password = rootProject.findProperty('ossrhPassword') ?: ''
            }
        }
    }
}

signing {
    required { isSnapshot }
    sign publishing.publications.mavenJava
}
